#
# .zshrc by chluna
#
# for `root` user, ln -s $ZDOTDIR  to /root
#                        ~/.zshenv
#

# zsh functions
autoload -Uz compinit; compinit
#autoload -Uz promptinit; promptinit

# completion for kitty
[[ -f $(which kitty) ]] && kitty + complete setup zsh | source /dev/stdin

# Powerlevel10k theme
# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
#if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
#  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
#fi
# load theme
[[ -f $ZDOTDIR/powerlevel10k/powerlevel10k.zsh-theme ]] && source $ZDOTDIR/powerlevel10k/powerlevel10k.zsh-theme
# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ -f $ZDOTDIR/.p10k.zsh ]] && source $ZDOTDIR/.p10k.zsh

# git completions
fpath=(/usr/share/git/completions $fpath)

# zsh autosuggestions
[[ -f $ZDOTDIR/zsh-autosuggestions/zsh-autosuggestions.zsh ]] && source $ZDOTDIR/zsh-autosuggestions/zsh-autosuggestions.zsh
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=#888888"

# zsh syntax highlighting
[[ -f $ZDOTDIR/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]] && source $ZDOTDIR/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

# key bindings
# ctrl + arrow to move by words
bindkey "^[[1;5C" forward-word
bindkey "^[[1;5D" backward-word
# make home & end work as it should
bindkey "^[[H" beginning-of-line
bindkey "^[[F" end-of-line

# alias
alias dir='dir --color=auto'
alias vdir='vdir --color=auto'
alias grep='grep --color=auto'
alias egrep='egrep --color=auto'
alias fgrep='fgrep --color=auto'
alias free='free -m'				# show sizes in MB
alias cp='cp -i --reflink=always'	# confirm before overwriting something
alias df='df -h'					# human-readable sizes
# custom aliases
alias c='vscodium'
alias d='cd $DOTFILES'
alias e='micro'
alias f='nnn'
alias l='journalctl -rb'
alias n='neofetch --colors 4 7 4 4 7 6 --ascii_colors 4 4'
# rust alternatives
if [[ -f $(which lsd) ]]; then
	alias ls='lsd -AF --group-dirs first'
	alias ll='lsd -aFl --group-dirs first'
	alias lt='lsd -A --tree --group-dirs first --depth'
fi
# global custom functions
pm() {
    case "$1" in
        i|install)	shift; paru -S "$@";;
        s|search)	shift; paru "$@";;
        r|remove)	shift; paru -Rns "$@";;
        p|purge)	pacman -Qtdq | sudo pacman -Rsn -;;
        m|mirror)	sudo reflector --verbose 		\
        			--save /etc/pacman.d/mirrorlist \
        			--protocol https 				\
        			--sort rate 					\
        			--fastest 50 					\
        			--latest 40 					\
        			--score 30 						\
        			--age 24;;
        e|export)
        	exp=$DOTFILES/pacman
        	[[ ! -d $exp ]] && mkdir -p $exp
            pacman -Qqen | awk '{print $1}' > $exp/pkgloc.txt
            pacman -Qqem | awk '{print $1}' > $exp/pkgaur.txt
            echo "Exported in $exp";;
        q|query)	shift; pacman -Qs "$@";;
        c|clean)	paru -Sc;;
        d|diff)		sudo DIFFPROG='delta --color-only' pacdiff;;
        u|update)	checkupdates;;
        up|upgrade)
        	log=/tmp/paru.script
        	script -q $log -c paru
        	[[ $(grep -E '.+\.pac(new|orig|save)' $log) ]] && pm diff
        	rm $log
        	pacman -Qtdq > /dev/null && pm purge
        	xfce4-panel --plugin-event=genmon-8:refresh:bool:true;;
        *)
        	echo "Usage: pm [install|query|remove] <pkg1 pkg2 ...>"
        	echo "       pm [clean|diff|export|purge|upgrade]"
    esac
}
zs() {
	case "$1" in
		e|edit)
			chksum=/tmp/zshrc.sha1
			sha1sum $ZDOTDIR/.zshrc > $chksum
			e $ZDOTDIR/.zshrc
			sha1sum -c $chksum --status
			if [[ $? -ne 0 ]] then
				rm $chksum
				read -q "choice?Reload changes? [y/N] "
				echo "\n"
				if [[ ${choice:l} = "y" ]] exec zsh
			fi
			rm $chksum;;
		u|upgrade)
			dir=$(pwd)
			cd $ZDOTDIR
			find . -mindepth 1 -maxdepth 1 -type d -print -exec git -C {} pull \;
			cd $pwd;;
		*)
			zs edit
	esac
}

if [[ $UID -ne 0 ]] then
	# rust
	rust() {
		file=$(echo $1 | cut -d'.' -f1)
		rustc $file.rs && ./$file && rm $file
	}
	# custom aliases
	alias sudo='sudo '
	alias y='sudo $(fc -ln -1)'
	# custom functions
	sc() {
		case "$1" in
		    edit)       sudo -E systemctl edit $2;;
			-h|--help)	echo "Usage: sc <systemctl command> <service name>";;
			*)			sudo systemctl $1 $2
		esac
	}
	od() {
	    case "$1" in
	        i|status)	journalctl --user-unit onedrive -f;;
	        r|start)	systemctl --user start onedrive;;
	        q|stop)		systemctl --user stop onedrive;;
	        x|restart)	systemctl --user restart onedrive;;
	        *)			echo "Usage: od [status|start|stop]"
        esac
	}
	bk() {
		case "$1" in
			c|create)
				sudo borg create --list -C zstd	/run/media/spike/ssd/borg::spike-{now:%Y%m%d}	\
				/home/spike
				bk prune;;
			p|prune)
				sudo borg prune -v --list				\
								   --keep-within=4d		\
								   --keep-daily=3		\
								   --keep-weekly=3		\
								   --keep-monthly=2 	\
								   /run/media/spike/ssd/borg;;
			*)	echo "Usage: bk [create|prune]"
		esac
	}
fi
